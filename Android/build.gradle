buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
    }
}

allprojects {
    repositories {
        google()
        jcenter()

        maven {
            url "https://atlas.microsoft.com/sdk/android"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencies {
}

ext {
    compileSdkVersion = 28
    targetSdkVersion = 28
    minSdkVersion = 26

    azureMapsVersion = '0.1'
    appCenterSdkVersion = '1.11.3'
    androidSupportVersion = '28.0.0'

    testInstrumentationRunner = 'android.support.test.runner.AndroidJUnitRunner'

    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
    azureMapsKey = properties.getProperty("azuremaps.key")
    appCenterKey = properties.getProperty("appcenter.key")
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            def config = android.defaultConfig
            if (android.compileSdkVersion == null)
                android.compileSdkVersion compileSdkVersion
            if (config.targetSdkVersion == null)
                config.targetSdkVersion targetSdkVersion
            if (config.minSdkVersion == null)
                config.minSdkVersion minSdkVersion
            if (config.testInstrumentationRunner == null)
                config.testInstrumentationRunner testInstrumentationRunner

            config.buildConfigField "String", "APPCENTER_KEY", "\"${appCenterKey}\""
            config.buildConfigField "String", "AZUREMAPS_KEY", "\"${azureMapsKey}\""

            android.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }
    }
}
